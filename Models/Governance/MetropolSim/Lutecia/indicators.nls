
;;;;;;;;;;;;;;;;;;;
;; Indicators of system performance
;;;;;;;;;;;;;;;;;;;

;-> convergence ? == stability ?
;-> distance moyenne deplacements == weighted mean by actives, employments of effective distance
;-> delta configs successives  ok diff-*
;-> hierarchy case, population, evol hierarchy.  -- TODO --


to update-indicators
  foreach tracked-indicators [
    let val run-result ?
    table:put history-indicators ? (lput val (table:get history-indicators ?))
  ]
end


to compute-indicators
  output-print (word "mean-accessibility : " mean-accessibility patches)
  output-print (word "mean-travel-distance : " mean-travel-distance)
  output-print (word "actives-entropy : " entropy-actives)
  output-print (word "collaboration wanted : " wanted-collaboration-rate)
  output-print (word "collaboration realized : " realized-collaboration-rate)
  output-print (word "externality-decrep : " externality-decrepitude)
  ;output-print (word "mean-congestion : " mean-congestion)
end

to compute-network-indicators
  network:cache-nw-measures
  ; total-nw-length mean-relative-speed mean-closeness-centrality mean-bw-centrality mean-path-length nw-diameter
  output-print (word "total-nw-length : " total-nw-length)
  output-print (word "mean-relative-speed : " mean-relative-speed)
  output-print (word "mean-closeness-centrality : " mean-closeness-centrality)
  output-print (word "mean-bw-centrality : " mean-bw-centrality)
  output-print (word "mean-path-length : " mean-path-length)
  output-print (word "nw-diameter : " nw-diameter)
end


;;
; TS indicators
to-report ts-indicator [indic]
  report table:get history-indicators indic
end




;;
;
to-report target-network-distance
  ; kill potential previous target network
  ;ask transportation-links with [status = "ghost"] [ask both-ends [die] die]
  ;report network:network-distances transportation-links with [status = "initial"] transportation-links with [status = "ghost"]
  report network:network-distances transportation-links ghost-transportation-links
end



;;
; mean accessibility of employments and actives
;  normalized by total quantities of employment and actives ? YES but not on patchset, all patches !
;
;  -- symmetric a-e --
to-report mean-accessibility [patchset]
  ifelse sum [actives] of patches > 0 and sum [employments] of patches > 0 and count patchset > 0 [
    report (sum [a-to-e-accessibility + e-to-a-accessibility] of patchset) / (2 * (sum [actives] of patches) * (sum [employments] of patches))
  ][report 0]
end

to-report overall-mean-accessibility
  report mean-accessibility patches
end

; min / max access
to-report max-accessibility
   report max [max (list a-to-e-accessibility e-to-a-accessibility)] of patches
end

to-report min-accessibility
   report min [min (list a-to-e-accessibility e-to-a-accessibility)] of patches
end


; mean travel distance
to-report mean-travel-distance
  report sum [a-to-e-distance + e-to-a-distance] of patches / (2 * (sum [actives] of patches) * (sum [employments] of patches))
end

; mean nw distance between nw patches
to-report mean-network-distance
  
end


; mean congestion in network
to-report mean-congestion
  report mean [congestion] of transportation-links
end

to-report mean-patch-congestion
  report mean patches-congestion
end


;; last variability of relocation process
to-report stability
  report ((diff-employments / sum [employments] of patches) + (diff-actives / sum [actives] of patches) ) / 2
end


to-report mean-flow
  report matrix:mean flow-matrix
end

to-report min-flow
  report first matrix:min flow-matrix
end

to-report max-flow
  report first matrix:max flow-matrix
end


;;;;
;; Morphological indicators


to-report moran-actives
  report morphology:moran 0
end

to-report entropy-actives
  report morphology:entropy 0
end

to-report slope-actives
  report first morphology:slope 0
end

to-report slope-rsquared-actives
  report last morphology:slope 0
end

to-report mean-distance-actives
  report morphology:distance 0
end

to-report moran-employments
  report morphology:moran 1
end

to-report entropy-employments
  report morphology:entropy 1
end

to-report slope-employments
  report first morphology:slope 1
end

to-report slope-rsquared-employments
  report last morphology:slope 1
end

to-report mean-distance-employments
  report morphology:distance 1
end


;;
; mean distance to centre (to measure sprawl)
to-report mean-distance-to-centre-actives
  let atot sum [actives] of patches
  report sum [actives / atot * distance (patch 0 0)] of patches
end

to-report mean-distance-to-centre-employments
  let etot sum [employments] of patches
  report sum [employments / etot * distance (patch 0 0)] of patches
end

to-report center-actives-prop
  report [actives] of patch 0 0 / sum [actives] of patches
end


to-report center-employments-prop
  report [employments] of patch 0 0 / sum [employments] of patches
end



;;
;  Network indicators


to-report table-values [t]
  let res []
  foreach table:keys t [set res lput table:get t ? res]
  report res
end

;;
; nw diameter
;  Normalized by world diag
to-report nw-diameter
  if count transportation-nodes = 0 [report 0]
  let diag sqrt (world-width ^ 2 + world-height ^ 2)
  report max map [sum map [[transportation-link-length] of ?] ?] (table-values shortest-paths) / diag
end


;;
; mean path length
;  Normalized by world diagonal
to-report mean-path-length
  if count transportation-nodes = 0 [report 0]
  let diag sqrt (world-width ^ 2 + world-height ^ 2)
  report (mean map [sum map [[transportation-link-length] of ?] ?] (table-values shortest-paths)) / diag
end

;;
;  bw centrality, normalized by number of paths considered
to-report mean-bw-centrality
  let n count transportation-nodes if n = 0 [report 0]
  report (mean [bw-centrality] of transportation-links) * 2 / (n * (n - 1))
end

to-report mean-closeness-centrality
  let n count transportation-nodes if n = 0 [report 0]
  report (mean [transportation-node-closeness-centrality] of transportation-nodes) / n
end


;;
; mean relative speed, already normalized
to-report mean-relative-speed
  if count transportation-nodes = 0 [report 0]
  report mean table-values nw-relative-speeds
end

;;
; nw length -> not normalized
to-report total-nw-length
  if count transportation-nodes = 0 [report 0]
  report sum [road-length] of transportation-links
end







;;
; Sum of employments in the external facility
to-report externality-employments
  ifelse external-facility = 0 or length external-facility = 0 [
    report 0 
  ][
    report sum [employments] of patches with [member? number external-facility]
  ]
end



to-report wanted-collaboration-rate
  ifelse length collaborations-wanted = 0 [
    report 0
  ][
    report sum collaborations-wanted / length collaborations-wanted
  ]
end


to-report realized-collaboration-rate
  ifelse length collaborations-wanted = 0 [
    report 0
  ][
    report sum collaborations-realized / length collaborations-realized
  ]
end

to-report expected-collaboration-rate
  ifelse length collaborations-wanted = 0 [
    report 0
  ][
    report mean collaborations-expected
  ]
end


to-report externality-decrepitude
  report externality-employments / (employments-max * ext-employments-proportion-of-max)
end
  
  
  
  
  
  
  
  
  
  
;; DEPRECATED using morphology extension

;; entropy of distribution of actives
;to-report actives-entropy
;  let actives-patches patches with [actives > 0]
;  let atot sum [actives] of actives-patches
;  report (- 1 / (ln count patches) * sum [actives / atot * ln (actives / atot)] of actives-patches)
;end

;; entropy of distribution of employments
;to-report employments-entropy
;  let employment-patches patches with [employments > 0]
;  let etot sum [employments] of employment-patches
;  report (- 1 / (ln count patches) * sum [employments / etot * ln (employments / etot)] of employment-patches)
;end  
  
  


