
;;;;;;;;;;;;;;
;; accessibility and utility functions
;;;;;;;;;;;;;;


;;
; update acc, form factor and utilities
to compute-patches-variables
  compute-patches-accessibilities
  compute-patches-form-factor
  compute-patches-utilities
  
  ; color patches here, for more understanding of the infrastructure explo process
  color-patches
  
end


;;
;  Compute patches accessibilities
;   given current distributions and effective distance matrix
to compute-patches-accessibilities
  ask patches [
     set prev-accessibility a-to-e-accessibility + e-to-a-accessibility
     ; only one csp for now
     set a-to-e-accessibility actives * scalar-product (patches-employments) map [exp (- lambda-accessibility * ?)] (matrix:get-row effective-distance-matrix number)
     set e-to-a-accessibility employments * scalar-product (patches-actives) map [exp (- lambda-accessibility * ?)] (matrix:get-row effective-distance-matrix number)
     
     set current-accessibility a-to-e-accessibility + e-to-a-accessibility
     
     ; update also travel distances
     set a-to-e-distance actives * scalar-product (patches-employments) (matrix:get-row effective-distance-matrix number)
     set e-to-a-distance employments * scalar-product (patches-actives) (matrix:get-row effective-distance-matrix number)
  ]
end

;;
; Compute form factor, as couples [a-form-factor,e-form-factor]
;
to compute-patches-form-factor
  ; only one csp : nothing to do
  ask patches [
    set form-factor (list actives-form-factor employments-form-factor)
  ]
end


;;
; patches utilities
;  note : could be in same loop as accessibilities ? more flexibility.
to compute-patches-utilities
  ask patches [
    set a-utility (a-to-e-accessibility ^ gamma-cobb-douglas) * ((first form-factor) ^ (1 - gamma-cobb-douglas))
    set e-utility (e-to-a-accessibility ^ gamma-cobb-douglas) * ((last form-factor) ^ (1 - gamma-cobb-douglas))
  ]
  
  ; renormalize utilities
  let ma-a max [a-utility] of patches let ma-e max [e-utility] of patches
  ask patches [set a-utility a-utility / ma-a set e-utility e-utility / ma-e]
end


;;
; form factor for actives :
;  they search to flee from density, both of actives and employments
to-report actives-form-factor
  report 1 / (actives * employments)
end


;;
; form factor for employments:
;  take only accessibility into account (for now)
to-report employments-form-factor
  report 1
end