
;;
;  Indicators


to compute-indicators
  network:cache-nw-measures
  output-print (word "diameter : " nw-diameter)
  output-print (word "path-length : " mean-path-length)
  output-print (word "centrality : " mean-bw-centrality)
  output-print (word "relative speed : " mean-relative-speed)
  output-print (word "length : " total-nw-length)
end



to indicators:update-indicators
  let current-time ticks
  foreach indicator-sample-patches [
     ask ? [
       table:put patch-values-table (list current-time "patch-population" pxcor pycor) patch-population
       table:put patch-values-table (list current-time "patch-distance-to-road" pxcor pycor) patch-distance-to-road
       table:put patch-values-table (list current-time "patch-closeness-centrality" pxcor pycor) patch-closeness-centrality
       table:put patch-values-table (list current-time "patch-bw-centrality" pxcor pycor) patch-bw-centrality
       table:put patch-values-table (list current-time "patch-accessibility" pxcor pycor) patch-accessibility
     ]
  ]
end



;;;;;
;; Micro correlations

;; in time

;;
; lagged correlation, between two vars, already
;  assumed var stored
to-report lagged-correlation [var1 var2 tau t0]
  let current-time ticks
  let x [] let y [] let firstvar var1 let secvar var2
  if tau > 0 [set firstvar var2 set secvar var1]
  let t (tau + 1 + t0)
  repeat (current-time - tau - 1 - t0) [
    foreach indicator-sample-patches [ask ? [
       set x lput ((table:get patch-values-table (list t firstvar [pxcor] of ? [pycor] of ?)) - (table:get patch-values-table (list (t - 1) firstvar [pxcor] of ? [pycor] of ?))) x
       set y lput ((table:get patch-values-table (list (t - tau) secvar [pxcor] of ? [pycor] of ?)) - (table:get patch-values-table (list (t - tau - 1) secvar [pxcor] of ? [pycor] of ?))) y
    ]]
    set t t + 1
  ]
  let mx mean x set x map [? - mx] x let my mean y set y map [? - my] y
  report mean (list-times-element-wise x y) / (standard-deviation x * standard-deviation y)
end


;;
; set of lagged corrs for a given pair of vars
to-report lagged-corrs [var1 var2 abstaumax t0]
  ; get var values
  let res []
  let tau (- abstaumax)
  repeat abstaumax + 1 [
    set res lput (lagged-correlation var2 var1 (abs tau) t0) res
    set tau tau + 1
  ]
  repeat abstaumax [
    set res lput (lagged-correlation var1 var2 tau t0) res
    set tau tau + 1
  ]
  report res
end


;;
; in space

;;
; correlation between two vars
to-report correlation [var1 var2 time]
  let x [] let y []
  foreach indicator-sample-patches [
    ask ? [
      set x lput (table:get patch-values-table (list time var1 [pxcor] of ? [pycor] of ?)) x
      set y lput (table:get patch-values-table (list time var2 [pxcor] of ? [pycor] of ?)) y
    ]
  ]
  let mx mean x set x map [? - mx] x let my mean y set y map [? - my] y
  report mean (list-times-element-wise x y) / (standard-deviation x * standard-deviation y)
end




;;
; Morphological indicators

to-report moran
  report morphology:moran 0
end

to-report entropy
  report morphology:entropy 0
end

to-report slope
  report first morphology:slope 0
end

to-report slope-rsquared
  report last morphology:slope 0
end

to-report mean-distance
  report morphology:distance 0
end


;;
;  Network indicators


;;
; nw diameter
;  Normalized by world diag
to-report nw-diameter
  let diag sqrt (world-width ^ 2 + world-height ^ 2)
  report max map [sum map [[road-length] of ?] ?] (table-values shortest-paths) / diag
end


;;
; mean path length
;  Normalized by world diagonal
to-report mean-path-length
  let diag sqrt (world-width ^ 2 + world-height ^ 2)
  report (mean map [sum map [[road-length] of ?] ?] (table-values shortest-paths)) / diag
end

;;
;  bw centrality, normalized by number of paths considered
to-report mean-bw-centrality
  let n count cities
  report (mean [bw-centrality] of roads) * 2 / (n * (n - 1))
end

;;
; nodes bw centrality
to-report mean-city-bw-centrality
  let n count cities
  report (mean [city-bw-centrality] of cities) * 2 / (n * (n - 1))
end

;;
; mean relative speed, already normalized
to-report mean-relative-speed
  report mean table-values nw-relative-speeds
end

;;
; nw length -> not normalized
to-report total-nw-length
  report sum [road-length] of roads
end





to export-nw [file-prefix]
  let cities-coords [(list xcor ycor who)] of cities
  let roads-ids [(list [who] of end1 [who] of end2)] of roads
  let cities-file (word file-prefix "_cities.csv")
  let roads-file (word file-prefix "_roads.csv")
  
  foreach cities-coords [
    print-in-file cities-file implode-with-delimiter ? ";"
  ]
  
  foreach roads-ids [
    print-in-file roads-file implode-with-delimiter ? ";"
  ]
  
end




